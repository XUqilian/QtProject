// SettingItemView.qml
import QtQuick 2.15
import QtQuick.Controls 2.15
import QtQuick.Dialogs
import QtQuick.Layouts 1.15
import localRS

Item {
    id: root

    // è¾“å…¥
    property SettingItem itemModel
    // é¼ æ ‡
    property bool hovered: false        // æ”¾åœ¨å¤–éƒ¨æ˜¯ä¸ºäº†ç›‘å¬è€Œéæ”¹åŠ¨
    // å¤–è§‚é…ç½®
    property font font: Qt.font({  pixelSize: 14 })

    property color textColor: "#E0E0E0" //888888
    property color infoTextColor: "B0B0B0"  // Qt.rgba(textColor.r, textColor.g, textColor.b, 0.7)
    property color theme: "#121212" // 000000   // èƒŒ
    property color borderColor: "#444444"       // è¾¹
    property color primaryColor: "#0078D7"      // ä¸»
    property color accentColor: "#FF9800"       // æ¬¡

    // ==================== å¤§å° ====================
    width: parent ? parent.width : 100
    height: columnLayout.implicitHeight  // è‡ªåŠ¨è®¡ç®—é«˜åº¦

    // ==================== é¼ æ ‡æ‚¬åœæ£€æµ‹ ====================
    MouseArea {
            anchors.fill: root
            hoverEnabled: true
            onEntered: root.hovered = true
            onExited: root.hovered = false
        }

    // ==================== èƒŒæ™¯ ====================
    Rectangle {
        id:rect
        anchors.fill: root
        color: theme
        border.color: hovered ? primaryColor : borderColor  // "#0078d7" : "#cc4771"
        border.width: hovered ? 2 : 0
        radius: 4
        clip: true  // é˜²æ­¢å†…å®¹æº¢å‡ºåœ†è§’

        // ==================== ä¸»å¸ƒå±€ï¼šå‚ç›´æ’åˆ— ====================
        ColumnLayout {
            id: columnLayout
            width: parent.width
            // height: implicitHeight
            spacing: 4

            // ç¬¬ä¸€è¡Œï¼šæ ‡é¢˜ï¼ˆå¤§ã€ç²—ï¼‰
            Label {
                text: itemModel?.title ?? ""
                font: Qt.font({
                        family: root.font ? root.font.family : undefined,
                        pixelSize: (root.font && root.font.pixelSize) ? root.font.pixelSize + 4 : 18,
                        bold: true
                })
                color: textColor
                wrapMode: Text.NoWrap
                maximumLineCount: 2
                elide: Text.ElideRight
                Layout.topMargin: 4
                Layout.alignment: Qt.AlignLeft

            }

            // ç¬¬äºŒè¡Œï¼šè¯´æ˜æ–‡å­—ï¼ˆå°ã€æ–œä½“ã€æµ…è‰²ï¼‰
            Label {
                text: itemModel?.info ?? ""
                font: Qt.font({
                    family: root.font ? root.font.family : undefined,
                    pixelSize: (root.font && root.font.pixelSize) ? root.font.pixelSize - 4 : 10,
                    italic: true
                })
                color: infoTextColor
                visible: itemModel ? (itemModel.info && itemModel.info.length > 0) : false
                wrapMode: Text.WordWrap
                maximumLineCount: 3
                elide: Text.ElideRight
                Layout.topMargin: 4
                Layout.bottomMargin: 8
                Layout.alignment: Qt.AlignLeft
            }

            // ç¬¬ä¸‰è¡Œï¼šåˆ†éš”ç¬¦
            Rectangle{
                height: 1
                // width: parent.width
                Layout.fillWidth: true
                Layout.leftMargin: 8
                Layout.rightMargin: 8
                color: "#ddd"
            }

            // ç¬¬å››è¡Œï¼šå†…å®¹åŒºï¼ˆå æ»¡å‰©ä½™å®½åº¦ï¼‰
            // Loader åŠ è½½ Component æ˜¯ç›´æ¥"æ›¿æ¢ä¸º"çš„æ–¹å¼ å¹¶ä¸”åœ¨loaderä¸­è®¾ç½®çš„å±æ€§ä¼š"æ›¿ä»£"åŠ è½½çš„ç»„ä»¶é¡¶çº§æ§ä»¶ä¸­çš„å¯¹åº”å±æ€§
            Loader {
                id: contentLoader

                // ä¸å¼ºåˆ¶å¡«æ»¡ï¼Œä½†å»ºè®®å®½åº¦æ¥è¿‘çˆ¶å®¹å™¨
                Layout.preferredWidth: parent ? parent.width - 16 : 300  // å‡å»å·¦å³ margin
                Layout.maximumWidth: parent ? parent.width - 16 : 400     // æœ€å¤§ä¹Ÿä¸èƒ½å¤ªå®½

                // å±…ä¸­å¯¹é½ï¼ˆå¯é€‰ï¼Œä¹Ÿå¯å·¦å¯¹é½ï¼‰
                Layout.alignment: Qt.AlignHCenter

                // è§†è§‰ç¼©è¿›
                Layout.leftMargin: 8
                Layout.rightMargin: 8
                Layout.bottomMargin: 4

                // é«˜åº¦è‡ªé€‚åº”
                height: contentLoader.item ? contentLoader.item.implicitHeight : 0

                sourceComponent: componentForType(itemModel.type)
            }

            // ç¬¬äº”è¡Œï¼šæ•´ä½“åˆ†éš”ç¬¦
            Rectangle{
                height: 3
                // width: parent.width
                Layout.fillWidth: true
                Layout.leftMargin: 2
                Layout.rightMargin: 2
                Layout.bottomMargin: 4
                color: "#00cc6a"    // 0078d4
            }
        }

    }


    // MouseAreaå¦‚æœæ”¾åœ¨è¿™é‡Œæ˜¯zè½´æœ€ä¸Šå±‚ï¼Œä¼šé˜»æŒ¡æ‰€æœ‰é¼ æ ‡äº‹ä»¶



    // ==================== ç±»å‹æ˜ å°„é€»è¾‘ï¼ˆä¿æŒä¸å˜ï¼‰====================
    function componentForType(type) {
        switch (type) {
            case SettingItem.Boolean:
                return boolComponent
            case SettingItem.Integer:
            case SettingItem.Number:
                return numberComponent
            case SettingItem.String:
                return stringComponent
            case SettingItem.TextArea:
                return textAreaComponent

            case SettingItem.FilePath:
                return fileComponent
            case SettingItem.DirPath:
                return dirComponent

            case SettingItem.Color:
                return colorComponent
            case SettingItem.Font:
                return fontComponent

            case SettingItem.Enum:
                return enumComponent
            case SettingItem.Flag:
                return flagComponent
            case SettingItem.List:
                return listComponent

            case SettingItem.Action:
                return actionComponent

            case SettingItem.Separator:
                return separatorComponent
            case SettingItem.Group:
                return groupComponent

            default:
                return defaultComponent
        }
    }

    // ==================== å„ Component å®šä¹‰ï¼ˆç•¥ï¼‰====================
    // å¤–ç•Œä½¿ç”¨è€…åº”å†³å®šè“å›¾æ˜¾ç¤ºçš„æœ€ç»ˆå¤§å°ï¼Œè€Œè“å›¾åº”ç¡®å®šè‡ªå·±çš„å®é™…å¤§å°(è®¾ç½®æœ€å°å®½é«˜ä»¥åŠimplicit)ä¸”è§„åˆ’å¥½å¸ƒå±€(ç»„ä»¶ç¬¦åˆç¾å­¦æ’åˆ—)
    Component {
        id: boolComponent
        Switch {
            checked: itemModel.value
            onCheckedChanged: itemModel.value = checked
        }
    }

    Component {
        id: numberComponent

        // å¸ƒå±€ç»„åˆ
        RowLayout {
            spacing: 10

            Slider {
                id: slider
                Layout.preferredWidth: 4
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 24

                value: itemModel.value
                from: itemModel.configs["from"] !== undefined ? itemModel.configs["from"] : 0
                to: itemModel.configs["to"] !== undefined ? itemModel.configs["to"] : 1
                stepSize: itemModel.configs["stepSize"] !== undefined
                       ? itemModel.configs["stepSize"]
                       : (itemModel.type === SettingItem.Integer ? 1 : 0.01)

                onValueChanged:  itemModel.value  = (itemModel.type === SettingItem.Integer) ? Math.round(value) : value

            }

            // TextFieldï¼šç”¨äºç²¾ç¡®è¾“å…¥
            TextField {
                Layout.preferredWidth: 1
                Layout.fillWidth: true
                color:textColor

                Layout.minimumWidth:20
                Layout.minimumHeight: 24

                text: itemModel.value.toLocaleString(Qt.locale(), "f", (itemModel.type === SettingItem.Integer) ? 0 : 2)

                validator: DoubleValidator {
                    bottom: (itemModel.configs["from"] !== undefined) ? itemModel.configs["from"] : -Infinity
                    top: (itemModel.configs["to"] !== undefined) ? itemModel.configs["to"] : Infinity
                }

                onAccepted: {
                    var input = Number.fromLocaleString(Qt.locale(),text)  // æ­£ç¡®è§£ææœ¬åœ°åŒ–æ•°å­—

                    if (!isNaN(input) && input >= 0.0 && input <= 1.0)
                    {
                        // æº¢å‡ºéªŒè¯
                        var min = (itemModel.configs["from"] !== undefined) ? itemModel.configs["from"] : -Infinity;
                        var max = (itemModel.configs["to"] !== undefined) ? itemModel.configs["to"] : Infinity;
                        if (input < min || input > max) return;

                        itemModel.value = (itemModel.type === SettingItem.Integer) ? Math.round(input) : input
                    }

                }

            }


        }
    }

    Component {
        id: stringComponent

        TextField {
            id: textField

            text: itemModel.value
            placeholderText: (itemModel.configs["placeholderText"] !== undefined) ? itemModel.configs["placeholderText"] : ""

            // è¾“å…¥éªŒè¯å™¨ï¼šå§‹ç»ˆå­˜åœ¨ï¼Œä½†å¯â€œå¤±æ•ˆâ€
            validator: RegularExpressionValidator  {
                regularExpression: {
                    var pattern = itemModel.configs["validator"];
                    if (pattern && typeof pattern === "string" && pattern.trim() !== "") {
                        try {
                            return new RegExp(pattern);
                        } catch (e) {
                            console.warn("Invalid regex:", pattern);
                            return /(?:)/;
                        }
                    }
                    return /(?:)/;
                }
            }

            echoMode: {
                let mode = itemModel.configs["echoMode"];

                // æ£€æŸ¥æ˜¯å¦ä¸º number ç±»å‹ï¼ˆä¸”æ˜¯æ•´æ•°ï¼‰
                if (typeof mode !== 'number' || !isFinite(mode) || Math.floor(mode) !== mode) {
                    console.warn(
                        `[Config Warning] 'echoModeInput' is not a valid integer enum.`,
                        `Expected int (0-3), got: '${mode}' (type: ${typeof mode})`,
                        `Item: ${itemModel.title || 'unknown'}`
                    );
                    return TextInput.Normal;
                }

                // æ£€æŸ¥æ˜¯å¦åœ¨åˆæ³•æšä¸¾èŒƒå›´å†… [0, 3]
                if (mode < 0 || mode > 3) {
                    console.warn(
                        `[Config Warning] 'echoModeInput' value out of range.`,
                        `Valid: 0=Normal, 1=Password, 2=PasswordEchoOnEdit, 3=NoEcho, got: ${mode}`,
                        `Item: ${itemModel.title || 'unknown'}`
                    );
                    return TextInput.Normal;
                }

                // å®‰å…¨ switchï¼ˆè™½ç„¶ mode æ˜¯ intï¼Œä½†ä»æ˜ç¡®å¤„ç†ï¼‰
                switch (mode) {
                    case TextInput.Password:
                        return TextInput.Password;
                    case TextInput.PasswordEchoOnEdit:
                        return TextInput.PasswordEchoOnEdit;
                    case TextInput.NoEcho:
                        return TextInput.NoEcho;
                    case TextInput.Normal: // æ˜ç¡®å¤„ç† 0
                    default:
                        return TextInput.Normal;
                }
            }

            onTextChanged: itemModel.value = text
        }
    }

    Component {
        id: textAreaComponent
        TextArea {            
            text: value
            placeholderText: (itemModel.configs["placeholderText"] !== undefined) ? itemModel.configs["placeholderText"] : ""
            wrapMode: Text.Wrap
            font.family: "Consolas, Courier, monospace"
            font.pixelSize: 12

            onTextChanged: itemModel.value = text
        }
    }


    Component {
        id: fileComponent
        RowLayout {
            spacing: 10

            TextField {
                Layout.preferredWidth: 4
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 32

                text: itemModel.value
                placeholderText:  itemModel.configs["placeholderText"] !== undefined ? itemModel.configs["placeholderText"] : ""
                readOnly: true  // æ¨èåªè¯»ï¼Œé˜²æ­¢è¯¯æ”¹
            }
            Button {
                Layout.preferredWidth: 1
                Layout.fillWidth: true

                Layout.minimumWidth:20
                Layout.minimumHeight: 32

                text: "ğŸ“Select File"
                onClicked: fileDialog.open()
            }

            FileDialog {
                id: fileDialog
                title: "Select File"
                fileMode: FileDialog.OpenFile

                selectedFile: Qt.resolvedUrl(itemModel.value)
                // æ­£ç¡®çš„ nameFilters æ ¼å¼
                nameFilters: itemModel.configs["nameFilters"] !== undefined ? itemModel.configs["nameFilters"] : []

                onAccepted: itemModel.value = selectedFile.toString()

            }
        }
    }

    Component {
        id: dirComponent
        RowLayout {
            spacing: 10

            TextField {
                Layout.preferredWidth: 4
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 32

                text: itemModel.value
                placeholderText: itemModel.configs["placeholderText"] !== undefined ? itemModel.configs["placeholderText"] : ""
                readOnly: true
            }
            Button {
                Layout.preferredWidth: 1
                Layout.fillWidth: true

                Layout.minimumWidth:20
                Layout.minimumHeight: 32

                text: "ğŸ“‚Select Dir"
                onClicked: dirDialog.open()
            }

            FolderDialog {
                id: dirDialog
                title: "Select Dir"
                selectedFolder: Qt.resolvedUrl(itemModel.value)

                onAccepted: itemModel.value = selectedFolder.toString()

            }
        }
    }

    Component {
        id: colorComponent

        RowLayout {
            spacing: 10

            Rectangle {
                Layout.preferredWidth: 3
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 32

                radius: 4
                color: itemModel.value
                border.width: 1
                border.color: "#ccc"
                Layout.alignment: Qt.AlignCenter
            }

            Button {
                Layout.preferredWidth: 2
                Layout.fillWidth: true

                Layout.minimumWidth:20
                Layout.minimumHeight: 32

                text: "ğŸ¨ Select Color"

                onClicked: colorDialog.open()
            }

            ColorDialog {
                id: colorDialog
                title: "Select Color"
                // æ³¨æ„ï¼šcolor æ˜¯ QColorï¼ŒitemModel.value åº”ä¸º Qt.rgba() æˆ– "#rrggbb"
                selectedColor: itemModel.value

                onAccepted: itemModel.value = selectedColor
            }
        }
    }

    Component {
        id: fontComponent

        RowLayout {
            spacing: 8

            // å­—ä½“é¢„è§ˆæ–‡æœ¬
            Label {
                Layout.preferredWidth: 3
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 32

                text: "Text"
                font: itemModel.value
            }

            Button {
                Layout.preferredWidth: 2
                Layout.fillWidth: true

                Layout.minimumWidth:20
                Layout.minimumHeight: 32

                // æ˜¾ç¤ºå­—ä½“ä¿¡æ¯
                text: itemModel.value ?
                    `font: ${itemModel.value.family}, ${itemModel.value.pointSize}pt` :
                    "Select Font"

                onClicked: fontDialog.open()
            }

            FontDialog {
                id: fontDialog
                title: "Select Font"
                selectedFont: itemModel.value

                onAccepted: itemModel.value = selectedFont
            }
        }

    }


    Component {
        id: enumComponent
        ComboBox {
            model: itemModel.options
            currentIndex: itemModel.options.indexOf(itemModel.value.toString())
            editable: false

            onCurrentIndexChanged:
            {
                if (currentIndex >= 0 && currentIndex < count) {
                       // å®‰å…¨è·å–å½“å‰é¡¹çš„æ–‡æœ¬
                       var selectedValue = textAt(currentIndex);    //model[currentIndex];
                       itemModel.value = selectedValue;
                   }

                // itemModel.value = currentText    // è¿™ç§æ–¹å¼ä¼šæœ‰ä¸€ç¬é—´çš„ç©ºç™½å¯¼è‡´æ— æ³•è§¦å‘
            }
        }
    }


    Component {
        id: flagComponent

        RowLayout {
            id: column
            spacing: 4

            // å­˜å‚¨æ¯ä¸ª checkbox çš„é€‰ä¸­çŠ¶æ€ï¼ˆå¸ƒå°”æ•°ç»„ï¼‰
            property var checkedState: []

            Repeater {
                Layout.preferredWidth: 3
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 32

                id: repeater
                model: itemModel.options  // ["Option A", "Option B", ...]

                delegate: CheckBox {
                        width: parent.width
                        anchors.margins: 8
                        spacing: 8

                        text: modelData
                        // ç»‘å®šåˆ°å¤–éƒ¨çŠ¶æ€æ•°ç»„
                        checked: column.checkedState[index]
                        onCheckedChanged: column.checkedState[index] = checked
                    }

            }

            // ä¿å­˜æŒ‰é’®ï¼šæ›´æ–° itemModel.value
            Button {
                Layout.preferredWidth: 2
                Layout.fillWidth: true

                Layout.minimumWidth:80
                Layout.minimumHeight: 32

                text: "Save"
                onClicked: {
                    itemModel.value = getChecks()  // ç›´æ¥èµ‹å€¼å­—ç¬¦ä¸²æ•°ç»„
                }
            }


            // --- å…¬å…±å‡½æ•°ï¼šç»Ÿä¸€å…¥å£/å‡ºå£ ---

            // åˆå§‹åŒ–ï¼šä½¿ç”¨ itemModel.value è®¾ç½®å“ªäº›é¡¹è¢«é€‰ä¸­
            function setChecks(selectedOptions) {
                if (!Array.isArray(selectedOptions)) return

                for (var i = 0; i < itemModel.options.length; i++) {
                    // å¦‚æœå½“å‰é€‰é¡¹æ–‡æœ¬åœ¨ selectedOptions ä¸­ï¼Œåˆ™å‹¾é€‰
                    column.checkedState[i] = selectedOptions.includes(itemModel.options[i])
                }
            }

            // è·å–å½“å‰é€‰ä¸­é¡¹ï¼ˆè¿”å›å­—ç¬¦ä¸²æ•°ç»„ï¼‰
            function getChecks() {
                var result = []
                for (var i = 0; i < itemModel.options.length; i++) {
                    if (column.checkedState[i]) {
                        result.push(itemModel.options[i])
                    }
                }
                return result
            }

            // === è‡ªåŠ¨åˆå§‹åŒ–ï¼šä½¿ç”¨ itemModel.value ===
            Component.onCompleted: {
                // åˆå§‹åŒ–çŠ¶æ€æ•°ç»„
                column.checkedState = new Array(itemModel.options.length).fill(false)

                // ä½¿ç”¨ itemModel.value åˆå§‹åŒ–é€‰ä¸­çŠ¶æ€
                var initialValues = itemModel.value || []
                if (Array.isArray(initialValues)) {
                    setChecks(initialValues)
                } else {
                    console.warn("itemModel.value should be an array of strings. Got:", typeof initialValues)
                    setChecks([])
                }
            }
        }
    }

    Component {
        id: listComponent

        ColumnLayout{
            spacing: 8

            //ä¸´æ—¶æ•°æ®ï¼ˆç¼–è¾‘åŒºï¼‰
            property var tempValue: []

            //åˆå§‹åŒ–ï¼šä» itemModel.value åŠ è½½
            Component.onCompleted: {
                tempValue = (itemModel.value && Array.isArray(itemModel.value))
                           ? itemModel.value.slice()
                           : [];
            }

            function cleanupNewItems(list) {
                var pattern = /^New Item\s*\d*$/;  // åŒ¹é… "æ–°æ¡ç›®", "æ–°æ¡ç›®1", "æ–°æ¡ç›® 2" ç­‰
                var i = list.length;
                while (i--) {
                    if (pattern.test(list[i]) || list[i] === "") {
                        list.splice(i, 1);  // å€’åºåˆ é™¤ï¼Œé¿å…ç´¢å¼•é”™ä¹±
                    }
                }
            }

            RowLayout {
                spacing: 8
                Layout.fillWidth: true

                Button {
                    text: "Add"
                    onClicked: tempValue.push("New Item")
                }
                Button {
                    text: "Save"
                    onClicked: {
                        cleanupNewItems(tempValue)
                        itemModel.value = tempValue;
                    }
                }
            }

            //å¯ç¼–è¾‘åˆ—è¡¨ï¼ˆç›´æ¥ç»‘å®š tempValueï¼‰
            ColumnLayout {
                spacing: 4
                Layout.fillWidth: true

                Repeater {
                    model: tempValue
                    delegate:
                        RowLayout {
                            Layout.fillWidth: true
                            spacing: 8

                            TextField {
                                Layout.fillWidth: true
                                Layout.minimumWidth:80
                                Layout.minimumHeight: 32

                                implicitHeight: 32

                                text: modelData

                                onTextChanged: tempValue[index] = text
                            }

                            Button {
                                Layout.preferredWidth: 32
                                Layout.preferredHeight: 32

                                text: "X"

                                onClicked: tempValue.splice(index, 1)
                            }
                        }

                }
            }
        }
    }


    Component {
        id: actionComponent
        Button {
            text: "Exec" // itemModel.title
            onClicked: itemModel.value = true
            // å¯ emit signal: itemModel.buttonClicked()
            // ç›´æ¥itemModel.value = true; è¿™é‡Œçš„å€¼ç±»å‹éœ€è¦é€‚é… åœ¨WRITEå‡½æ•°ä¸­å®Œå–„æ‰§è¡Œçš„Action
        }
    }


    Component {
        id: separatorComponent
        Rectangle {
            height: 2; width: parent.width
            color: "#ddd"
        }
    }

    Component {
        id: groupComponent
        Label {
            text: "ã€" + itemModel.title + "ã€‘"
            font.bold: true; color: "#555"
            horizontalAlignment: Text.AlignHCenter
        }
    }


    Component {
        id: defaultComponent
        Label {
            text: "âš  Unknown Type: " + itemModel.type
            color: "red"
        }
    }
}
